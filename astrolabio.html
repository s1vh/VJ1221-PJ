<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8">
    <title> Informatica Grafica </title>
    <style>
      canvas {border: 1px solid black;}
    </style>
  </head>
  
  <body>
    
    <canvas id="myCanvas" width="800" height="800">
      El Navegador no soporta HTML5
    </canvas>
    
	<!--//	VERTEX SHADER	------------------------------------------------------------->
    
	<!--Gouraud Vertex Shader-->
	<script id="GouraudVertexShader" type="x-shader/x-vertex">
      
      uniform   mat4 projectionMatrix;
      uniform   mat4 modelViewMatrix;
      uniform   mat3 normalMatrix;
      
      attribute vec3 VertexPosition;
      attribute vec3 VertexNormal;
      
      varying   vec3 colorOut;

      struct LightData {
        vec3 Position; // Posición en coordenadas del ojo
        vec3 La;       // Ambiente
        vec3 Ld;       // Difusa
        vec3 Ls;       // Especular
      };
      uniform LightData Light;

      struct MaterialData {
        vec3 Ka;       // Ambiente
        vec3 Kd;       // Difusa
        vec3 Ks;       // Especular
        float alpha;   // Brillo
      };
      uniform MaterialData Material;
      
      vec3 phong (vec3 N, vec3 L, vec3 V) {
      
        vec3  ambient  = Material.Ka * Light.La;
        vec3  diffuse  = vec3(0.0);
        vec3  specular = vec3(0.0);
        
        float NdotL    = dot (N,L);
        
        if (NdotL > 0.0) {
          vec3  R       = reflect(-L, N);;
          float RdotV_n = pow(max(0.0, dot(R,V)), Material.alpha);
          
          diffuse  = NdotL   * (Light.Ld * Material.Kd);
          specular = RdotV_n * (Light.Ls * Material.Ks);
        }
        
        return (ambient + diffuse + specular);
        
      }
      
      void main()  {
      
        vec3 N          = normalize(normalMatrix * VertexNormal);
        vec4 ecPosition = modelViewMatrix * vec4(VertexPosition,1.0);
        vec3 ec         = vec3(ecPosition);
        vec3 L          = normalize(Light.Position - ec);
        vec3 V          = normalize(-ec);
        
        colorOut        = phong(N,L,V);
        
        gl_Position     = projectionMatrix * ecPosition;
      
      }
      
    </script>
	
	<!--Phong Vertex Shader-->
	<script id="PhongVertexShader" type="x-shader/x-vertex">
		
		uniform   mat4 projectionMatrix;
		uniform   mat4 modelViewMatrix;
		uniform   mat3 normalMatrix;
		
		attribute vec3 VertexPosition;
		attribute vec3 VertexNormal;
		
		varying vec3 N, ec;
		
		void main()	{
			
			N  = normalize(normalMatrix * VertexNormal);
			vec4 ecPosition= modelViewMatrix * vec4(VertexPosition,1.0);
			ec = vec3(ecPosition);
			
			gl_Position = projectionMatrix * ecPosition;
			
		}
		
	</script>	
    
	<!--//	FRAGMENT SHADER	----------------------------------------------------------->
	
	<!--Gouraud Fragment Shader-->
    <script id="GouraudFragmentShader" type="x-shader/x-fragment">
      
      precision mediump float;
      
      varying vec3 colorOut;

      void main() {
      
        gl_FragColor = vec4(colorOut,1);
      
      }
      
    </script>
	
	<!--Phong Fragment Shader-->
	<script id="PhongFragmentShader" type="x-shader/x-fragment">
		
		precision mediump float;
		
		struct LightData	{
			vec3 Position; // Posición en coordenadas del ojo
			vec3 La;       // Ambiente
			vec3 Ld;       // Difusa
			vec3 Ls;       // Especular
		};
		uniform LightData Light;
		
		struct MaterialData	{
			vec3 Ka;       // Ambiente
			vec3 Kd;       // Difusa
			vec3 Ks;       // Especular
			float alpha;   // Brillo
		};
		uniform MaterialData Material;
		
		varying vec3 N, ec;
		
		vec3 phong (vec3 N, vec3 L, vec3 V)	{
		
			vec3  ambient  = Material.Ka * Light.La;
			vec3  diffuse  = vec3(0.0);
			vec3  specular = vec3(0.0);
			
			float NdotL    = dot (N,L);
			
			if (NdotL > 0.0)	{
				vec3  R       = reflect(-L, N);
				float RdotV_n = pow(max(0.0, dot(R,V)), Material.alpha);
				
				diffuse  = NdotL   * (Light.Ld * Material.Kd);
				specular = RdotV_n * (Light.Ls * Material.Ks);
			}
			
			return (ambient + diffuse + specular);	
		}
		
		void main()	{
			
			vec3 n = normalize(N);
			vec3 L = normalize(Light.Position - ec);
			vec3 V = normalize(-ec);
			
			gl_FragColor = vec4(phong(n,L,V),1.0);
			
		}
		
	</script>
    
    <script src="gl-matrix-min.js"></script>
    <script src="materiales.js"></script>
    <script src="primitivas.js"></script>
    <script src="astrolabio.js"></script>

  </body>
</html>
